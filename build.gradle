plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.gradle.plugin-publish' version '0.9.10'
}

targetCompatibility = 1.7


repositories {
    jcenter()
}


sourceSets {
    integrationTest
}


configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}


dependencies {
    testImplementation('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testImplementation 'org.hamcrest:java-hamcrest:2.0.0.0'

    testRuntimeOnly 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.30'
}


gradlePlugin {
    testSourceSets sourceSets.integrationTest
}


task integrationTest(type: Test) {
    dependsOn pluginUnderTestMetadata
    group = 'verification'
    description = 'Runs the integration tests.'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}


apply from: "$rootDir/publishing.gradle"
